buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
  }
}

apply plugin: 'groovy'
apply plugin: "jacoco"
apply plugin: 'project-report'
apply plugin: 'nu.studer.plugindev'

ext {
  relUsername = project.hasProperty('bintray_username') ? project.getProperty('bintray_username') : 'admin'
  relPassword = project.hasProperty('bintray_password') ? project.getProperty('bintray_password') : 'password'
}

repositories {
  jcenter()
}

group = 'com.github.adrianbk'
version = '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.6

sourceSets {
  integrationTest {
    groovy.srcDir file('src/integTest/groovy')
    resources.srcDir file('src/integTest/resources')
    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
    runtimeClasspath = output + compileClasspath
  }
}

dependencies {
  compile localGroovy()
  compile gradleApi()

  compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
    exclude(module: 'groovy')
  }

  testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
    exclude module: 'groovy-all'
  }
  testCompile 'cglib:cglib-nodep:3.1'
}

task integrationTest(type: Test) {
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports.getHtml().destination = file("$test.reports.html.destination/integTest")
  reports.getJunitXml().destination = file("$test.reports.junitXml.destination/integTest")
}

jacoco {
  toolVersion = "0.7.2.201409121644"
}

check.dependsOn integrationTest
check.dependsOn jacocoTestReport

plugindev {
  pluginImplementationClass "com.github.adrianbk.tcitrigger.plugin.TravisciTriggerPlugin"
  pluginDescription 'A Gradle plugin to trigger parametrized Travis CI builds belonging to another GitHub repo.'
  pluginLicenses 'Apache-2.0'
  pluginTags 'gradle', 'plugin', 'travisci', 'environment variables'
  authorId 'adrianbk'
  authorName 'Adrian Kelly'
  authorEmail 'adrianbkelly+gplugin@gmail.com'
  projectUrl 'https://github.com/adrianbk/gradle-travisci-trigger-plugin'
  projectInceptionYear '2014'
  done()
}

bintray {
  user = "$relUsername"
  key = "$relPassword"
  pkg.repo = 'maven'
}